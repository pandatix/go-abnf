cpe-name        = "cpe:/" component-list
component-list  = part ":" vendor ":" product ":" version ":" update ":"
                  edition ":" lang
component-list /= part ":" vendor ":" product ":" version ":" update ":"
                  edition
component-list /= part ":" vendor ":" product ":" version ":" update
component-list /= part ":" vendor ":" product ":" version
component-list /= part ":" vendor ":" product
component-list /= part ":" vendor
component-list /= part
component-list /= empty
part            = "h" / "o" / "a" / empty
vendor          = string
product         = string
version         = string
update          = string
edition         = string
lang            = LANGTAG / empty
string          = *( unreserved / pct-encoded )
empty           = ""
unreserved      = ALPHA / DIGIT / "-" / "." / "_" / " Ìƒ"
pct-encoded     = "%" HEXDIG HEXDIG
ALPHA           = %x41-5a / %x61-7a   ; A-Z or a-z
DIGIT           = %x30-39  ; 0-9
HEXDIG          = DIGIT / "a" / "b" / "c" / "d" / "e" / "f"
LANGTAG         = cf. [RFC5646]
