avstring     = ( body / ( spec_chrs *body2 )) *1spec_chrs
spec_chrs    = 1*spec1 / spec2
spec1        = "?"
spec2        = "*"
body         = ( body1 *body2 ) / 2*body2
body1        = unreserved / quoted1
body2        = unreserved / quoted2
unreserved   = ALPHA / DIGIT / "_"
quoted1      = escape (escape / special / punc-no-dash)
quoted2      = escape (escape / special / punc-w-dash)
escape       = "\"
special      = spec1 / spec2
dash         = "-"
punc-no-dash = "!" / DQUOTE / "#"/ "$"/ "%" / "&" / "'" /
               "(" / ")" / "+" / "," / "." / "/" /
               ":" / ";" / "<" / "=" / ">" / "@" / "[" /
               "]" / "^" / "`" / "{" / "|" / "}" / "~"
punc-w-dash  = punc-no-dash / dash
DQUOTE       = %x22 ; double quote
ALPHA        = %x41-5a / %x61-7A   ; A-Z or a-z
DIGIT        = %x30-39    ; 0-9
