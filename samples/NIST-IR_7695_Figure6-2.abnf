cpe-name        = "cpe:/" component-list
component-list  = part ":" vendor ":" product ":" version ":" update ":"
                  edition ":" lang
component-list /= part ":" vendor ":" product ":" version ":" update ":"
                  edition
component-list /= part ":" vendor ":" product ":" version ":" update
component-list /= part ":" vendor ":" product ":" version
component-list /= part ":" vendor ":" product
component-list /= part ":" vendor
component-list /= part
component-list /= empty
part            = "h" / "o" / "a" / empty
vendor          = string
product         = string
version         = string
update          = string
edition         = string / packed
lang            = LANGTAG / empty
string          = str_wo_special / str_w_special
str_wo_special  = *( unreserved / pct-encoded )
str_w_special   = *1spec_chrs 1*( unreserved / pct-encoded ) *1spec_chrs
spec_chrs       = 1*spec1 / spec2
packed          = "~" string "~" string "~" string "~" string "~" string
empty           = ""
unreserved      = ALPHA / DIGIT / "-" / "." / "_"
special         = spec1 / spec2
spec1           = "%01"
spec2           = "%02"
pct-encoded     = "%21" / "%22" / "%23" / "%24" / "%25" / "%26" / "%27" /
                = "%28" / "%29" / "%2a" / "%2b" / "%2c" / "%2f" / "%3a" /
                = "%3b" / "%3c" / "%3d" / "%3e" / "%3f" / "%40" / "%5b" /
                = "%5c" / "%5d" / "%5e" / "%60" / "%7b" / "%7c" / "%7d" /
                = "%7e"
ALPHA           = %x41-5a / %x61-7a  ; A-Z or a-z
DIGIT           = %x30-39  ; 0-9
LANGTAG         = language ["-" region] ; cf. [RFC5646]
language        = 2*3ALPHA              ; shortest ISO 639 code
region          = 2ALPHA / 3DIGIT       ; ISO 3166-1 code or UN M.49 code
